plugins {
    id 'application'
}

repositories {
    mavenCentral()
}

configurations {
    jrubyc
    rspec
}

def jrubyComplete = 'org.jruby:jruby-complete:9.3.3.0'

dependencies {
    // guava is just a test dependency to demonstrate calling java from jruby
    implementation libs.guava
    implementation jrubyComplete
    jrubyc jrubyComplete
}

def generatedSrcDir = "$buildDir/generated-src/ruby"

task ensureDirectoriesExist {
    doLast {
        file(generatedSrcDir).mkdirs()
    }
}

task compileRuby(type: JavaExec) {
    dependsOn ensureDirectoriesExist

    classpath = configurations.jrubyc
    main = 'org.jruby.Main'
    args = ['-S', 'jrubyc', '--java', '-t', generatedSrcDir, 'src/main/ruby']

    jvmArgs = [
        '--add-opens', 'java.base/sun.nio.ch=ALL-UNNAMED',
        '--add-opens', 'java.base/java.io=ALL-UNNAMED',
        '-Dfile.encoding=UTF-8',
        '-Djruby.compile.mode=JIT'
    ]

    systemProperty 'line.separator', '\n'
    systemProperty 'jruby.compile.mode', 'ON'
}

compileJava {
    dependsOn compileRuby
    options.encoding = 'UTF-8'
}

sourceSets {
    main {
        java {
            srcDir 'src/main/java'
            // generated Java files are not found unless the generation directory is added to source sets
            srcDir generatedSrcDir
        }
    }
}

// TODO: rspec integration has not been thought out yet
testing {
    suites {
        test {
            useJUnit('4.13.2')
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17) // new jruby versions support 17 or 21
    }
}

application {
    mainClass = 'com.zebtech.App'
}

